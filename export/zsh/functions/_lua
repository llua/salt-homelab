#compdef lua lua5.1 lua51 lua-5.1 lua5.2 lua52 lua-5.2
# TODO: don't ignore /usr/share/lua/5.2/?/init.lua
local context state state_descr line
typeset -A opt_args

_lua_normal() {
  local expl
  if [[ -z "$opt_args[(I)-e]" ]]; then
    shift words
    (( CURRENT-- ))
  fi
  _normal
}

_arguments -S : \
  '-e+[execute statement]:statement:' \
  '-i[enter interactive mode after executing script]' \
  '*-l+[load module before executing script]:module:->module' \
  '-v[show version number]' \
  '-E[ignore environment variables]' \
  '1:lua script: _files' \
  '*:: := _lua_normal'

if [[ $state == module ]]; then
  local expl
  _wanted modules expl 'Modules' \
    compadd - ${(@)~^${(s.;.)"$($service -e 'print(package.path)')"}/\?/*}~*init.lua(N:t:s/.lua//)
fi
